/* Copyright (c) 2020 Software AG, Darmstadt, Germany and/or its licensors

 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied.
 * See the License for the specific language governing permissions and limitations under the License.
 */

package com.apamax.test;

using com.apama.correlator.Component;
using com.apama.correlator.Logging;
using com.apama.exceptions.Exception;

event AssertHelper {

	/**
	 * Logs <message> at ERROR.
	 * @param message - the message to log.
	 */
	static action logAtError(string message) {
		log message at ERROR;
	}

	/**
	 * Logs <message> at ERROR then throws a FailedAssertException.
	 * @param message - the message to log and to go with the exception.
	 */
	static action throwWithError(string message) {
		// In case the exception is caught by user code and silently ignored.
		logAtError(message);
		// If this is uncaught it will be logged at error by default and the monitor will terminate.
		throw Exception(message, "FailedAssertException");
	}

	/**
	 * Logs <message> at ERROR then terminates the monitor with the die statement.
	 * @param message - the message to log.
	 */
	static action dieWithError(string message) {
		logAtError(message);
		die;
	}

	static action isConfigEnabled(string key) returns boolean {
		string configAsserts := Component.getConfigProperties().getOr(key, "");
		return configAsserts.toLower() = true.toString().toLower();
	}

	/**
	 * Returns true if the monitor this is called from is configured to log at DEBUG (or TRACE).
	 */
	static action isLoggingAtDebug() returns boolean {
		boolean result := false;
		try {
			context.throwStdException();
		} catch (com.apama.exceptions.Exception e) {
			sequence<com.apama.exceptions.StackTraceElement> trace := e.getStackTrace();
			string caller := trace[1].getTypeName();
			string level := Logging.getApplicationLogLevel(caller);
			result := level = "DEBUG" or level = "TRACE";
		}
		return result;
	}

}
