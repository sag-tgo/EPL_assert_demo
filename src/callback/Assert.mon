/* Copyright (c) 2020 Software AG, Darmstadt, Germany and/or its licensors

 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied.
 * See the License for the specific language governing permissions and limitations under the License.
 */

package com.apamax.test;

/**
 * An assertion event to help with testing EPL applications.
 * Instances of this event that are created in any way other than using the create action
 * will be lazily initialized to log failed assertions at ERROR.
 * To change the default behavior from that provided by lazy initialisation, or to
 * explicitly declare the behavior, use the create action to create Assert event instances.
 *
 * @version 0.1
 */
event Assert {

	/**
	 * @private
	 * Used for lazy initialisation.
	 */
	boolean initialized;

	/**
	 * An action which takes one string paremeter which is invoked if an assertion fails.
	 */
	action<string> onFailedAssertion;

	/**
	 * Enabled/disabled flag.
	 * This allows an optimisation when asserts are disabled and
	 * gives the option of turning asserts on/off at runtime.
	 */
	boolean enabled;

	/**
	 * @private
	 * Lazy initialisation action.
	 */
	action init() {
		if not initialized {
			initialized := true;
			enabled := true;
			onFailedAssertion := logAtError;
		}
	}

	/**
	 * Create an instance of an Assert event.
	 * @param enabled - true = enabled, false = disabled.
	 * @param failedAssertCallback - The action to invoke if an assertion fails.
	 */
	static action createOpt(boolean enabled, action<string> failedAssertCallback) returns Assert {
		Assert result := new Assert;
		result.initialized := true;
		result.enabled := enabled;
		result.onFailedAssertion := failedAssertCallback;
		return result;
	}

	/**
	 * Create an instance of an Assert event. The instance will be enabled.
	 * @param failedAssertCallback - The action to invoke if an assertion fails.
	 */
	static action create(action<string> failedAssertCallback) returns Assert {
		return createOpt(true, failedAssertCallback);
	}

	/**
	 * @private
	 * Logs <message> at ERROR.
	 * @param message - the message to log.
	 */
	static action logAtError(string message) {
		log message at ERROR;
	}

	/**
	 * Fail an assertion.
	 * @param message the reason for the assertion failure.
	 */
	action fail(string message) {
		init();
		if not enabled {
			return;
		}

		onFailedAssertion("Assertion failed: " + message);
	}

	/**
	 * Assert that a boolean value is true.
	 * @param value the value that is being asserted is true.
	 * @param message description of what's being asserted.
	 */
	action isTrue(boolean value, string message) {
		init();
		if not enabled {
			return;
		}

		if not value {
			fail(message);
		}
	}

	/**
	 * Assert that a boolean value is false.
	 * @param value the value that is being asserted is false.
	 * @param message description of what's being asserted.
	 */
	action isFalse(boolean value, string message) {
		init();
		if not enabled {
			return;
		}

		if value {
			fail(message);
		}
	}

	/**
	 * Assert that two values are equal.
	 * Note: This is a deep equality check because EPL's = and != operator
	 * are deep equality operators.
	 * @param actual the actual value.
	 * @param expected the expected value.
	 * @param message description of what's being asserted.
	 */
	action equal(any actual, any expected, string message) {
		init();
		if not enabled {
			return;
		}

		if actual != expected {
			fail(message);
		}
	}

	/**
	 * Assert that two values are not equal.
	 * Note: This is a deep equality check because EPL's = and != operator
	 * are deep equality operators.
	 * @param actual the actual value.
	 * @param unexpected the unexpected value.
	 * @param message description of what's being asserted.
	 */
	action notEqual(any actual, any unexpected, string message) {
		init();
		if not enabled {
			return;
		}

		if actual = unexpected {
			fail(message);
		}
	}

	/**
	 * Assert that 'actual' is greater than 'min'.
	 * @param actual the actual value
	 * @param min the value that 'actual' should be greater than.
	 * @param message description of what's being asserted.
	 */
	action isGreaterThan(any actual, any min, string message) {
		init();
		if not enabled {
			return;
		}

		if not actual > min {
			fail(message);
		}
	}

	/**
	 * Assert that 'actual' is less than 'max'.
	 * @param actual the actual value
	 * @param max the value that 'actual' should be less than.
	 * @param message description of what's being asserted.
	 */
	action isLessThan(any actual, any max, string message) {
		init();
		if not enabled {
			return;
		}

		if not actual < max {
			fail(message);
		}
	}

	/**
	 * Assert that a value is within a range (inclusive).
	 * @param value the value that should be within the range.
	 * @param min the minimum value.
	 * @param max the maximum value.
	 * @param message description of what's being asserted.
	 */
	action isInRange(any value, any min, any max, string message) {
		init();
		if not enabled {
			return;
		}

		if not (value >= lower and value <= upper) {
			fail(message);
		}
	}
}
